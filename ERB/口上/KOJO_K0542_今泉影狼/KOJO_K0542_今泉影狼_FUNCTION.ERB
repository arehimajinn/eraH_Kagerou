;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:KOJO_K0542_今泉影狼_FUNCTION.ERB
;	Facility	:キャラクター口上(No.542:今泉影狼)
;
;	Licence		:ライセンス詳細を参照。
;
;	口上テンプレート version :0.017
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2016/07/11		暇人					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;-------------------------------------------------------------------------------
; 【フラグ使用状況】
;  ※CFLAG:500〜999まで自由に使用可能
;
;	ファイル内未使用
;
;-------------------------------------------------------------------------------

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- 独自関数
;
;	・ KOJO_IDENTIFY_K542				キャラ口上判別式中関数：存在チェック
;	・ WORD_K542						キャラ用地の文関数:表現
;	・ IF_RAND_K542						キャラ用口上ランダム関数:分岐
;	・ IF_NTR_K542						寝取られ＆寝取らせ判定用関数：寝取らせ
;
;===============================================================================

;-------------------------------------------------------------------------------
;   独自関数
;-------------------------------------------------------------------------------

;=================================================
;   sub KOJO_IDENTIFY_K542（没）
;=================================================
;   キャラ口上:口上ID
;-------------------------------------------------
; Output:
;  RESULTS 				口上ID
;  RETURN				真偽値(ID有無)
;-------------------------------------------------
;   ここでIDを設定しておくことにより他作者さんが
;   この口上を確認できたり、俺が他のキャラ書いた時
;   使えるのじゃ。3P主体なら余計に便利。式中関数版
;   を採用。適当に作ったほうだけど。
;-------------------------------------------------
;@KOJO_IDENTIFY_K542
;RESULTS '= "FG40I9G8"
;RETURN 1

;=================================================
;   sub KOJO_IDENTIFY_K542
;=================================================
;   キャラ口上:口上ID
;-------------------------------------------------
; Output:
;  RETURNF				口上ID
;-------------------------------------------------
@KOJO_IDENTIFY_K542
#FUNCTIONS
RETURNF "FG40I9G8"

;--------------------------------------------------
;地の文ランダム関数@WORD_K542
;	引数：種類
;地の文の種類を指定すると、その種類に応じてランダムで地の文を返します
;輝夜口上のアイデアをそのままパクりました。ありがとうございました。
;--------------------------------------------------
@WORD_K542(ARGS)
#FUNCTIONS

SELECTCASE ARGS
	CASE "笑み"
		RETURNF RAND_SPLIT("満面の笑みで/微笑んで/その口を柔らかくしながら/花のような笑顔で/妖艶な笑顔で/笑顔を浮かべて")
	CASE "哀"
		RETURNF RAND_SPLIT("瞳/表情/顔/態度")
	CASE "胸"
		RETURNF RAND_SPLIT("大きな胸/豊満なそれ/ゆったりとした胸/大きく豊かな胸/張りのある大きな胸/服の上からでも主張する胸/綺麗に整った胸")
ENDSELECT

;--------------------------------------------------
;陥落ランダム分岐関数@IF_FALL_K542
;	引数：名前の通りの変数に追加したいRANDを入れる。
;	返り値：名前の通りの変数（素質, 刻印）がキャラにあるかを見て、
;			あればLOCALに指定された数を追加し、RAND:LOCALしたのを返す。
;
;--------------------------------------------------
@IF_FALL_K542(隷属, 陥落, 上位陥落)
#FUNCTION
#DIM 隷属
#DIM 陥落
#DIM 上位陥落

LOCAL = 0
SIF MARK:隷属刻印
	LOCAL += 隷属
SIF MARK:陥落刻印
	LOCAL += 陥落
SIF TALENT:上位陥落
	LOCAL += 上位陥落
RETURNF LOCAL == 0 ? 0 # RAND:LOCAL

;--------------------------------------------------
;口上ランダム関数@IF_RAND_K542
;	引数：なし
;	返り値：ランダムで0-8
;SELECTCASE IF_RAND_K542()のような形で呼び出すことを想定しています。
;具体的には刻印系でランダム分け
;0-2 隷属刻印
;3-5 陥落刻印
;6-7 淫乱刻印
;--------------------------------------------------
@IF_RAND_K542()
#FUNCTION
#DIM MARK_LOCAL, 10

VARSET MARK_LOCAL, 0
FOR LOCAL, 0, 3
	SIF MARK:隷属刻印 > LOCAL
		MARK_LOCAL:LOCAL = 1
	SIF MARK:陥落刻印 > LOCAL
		MARK_LOCAL:(LOCAL + 3) = 1
	;淫乱刻印Lv3は未実装のため2つのみ
	SIF MARK:淫乱刻印 > LOCAL && LOCAL <= 1
		MARK_LOCAL:(LOCAL + 6) = 1
NEXT

;全ての刻印が0ならば-1を返す
SIF SUMARRAY(MARK_LOCAL, 0, 8) == 0
	RETURNF -1

WHILE 1
	LOCAL = RAND:8
	;ランダムで取得した場所が刻印のレベルと合っているならばBREAK
	SIF MARK_LOCAL:LOCAL
		BREAK
WEND

RETURNF LOCAL

;--------------------------------------------------
;寝取らせ判定関数@IF_NTR_K542
;	引数：なし
;	返り値：寝取らせなら1
;--------------------------------------------------
@IF_NTR_K542()
#FUNCTION

;2で割ったあまりが1なら寝取らせ。寝取られから取り戻した後NTR状態で寝取らせ。その状態からもう一度取りもどしさらにNTRると寝取られ
RETURNF (CFLAG:ＮＴＲ再陥落回数 % 2)

;--------------------------------------------------
;改行用関数@NEWLINE(ARG:0, ARG:1)
;	引数：
;		ARG:0 改行する回数。
;		ARG:1 真ならばWAITをかける。
;	返り値:改行回数
;	コメント:本体にあってもいいやろこれ
;--------------------------------------------------
@NEWLINE(ARG:0, ARG:1 = 0)
FOR LOCAL, 0, ARG:0
	PRINTL
NEXT
SIF ARG:1
	WAIT
RETURN ARG:0
